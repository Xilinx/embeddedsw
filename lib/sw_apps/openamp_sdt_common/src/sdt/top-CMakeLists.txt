# Copyright (C) 2023 Advanced Micro Devices, Inc.  All rights reserved.
cmake_minimum_required (VERSION 3.0.2)
if (POLICY CMP0048)
  cmake_policy(SET CMP0048 NEW)
endif()

if (POLICY CMP0077)
  cmake_policy(SET CMP0077 NEW)
endif()

project (open_amp_apps C)

include(CheckSymbolExists)
check_symbol_exists(ARMR5 "xparameters_ps.h" HAS_ARMR5)
if (HAS_ARMR5)
  set (MACHINE "zynqmp_r5" CACHE STRING "" FORCE)
  set (CROSS_PREFIX "armr5-none-eabi-" CACHE STRING "" FORCE)
  set (CMAKE_SYSTEM_PROCESSOR "arm" CACHE STRING "" FORCE)
  set (PROJECT_MACHINE "zynqmp_r5" CACHE STRING "" FORCE)
  set( DEMO_LINK_FLAGS " -lmetal -lgcc" )

else()
    message( FATAL_ERROR "Invalid machine. CMake will exit" )
endif()

set (OPENAMP_ROOT_DIR "${CMAKE_CURRENT_SOURCE_DIR}")
set (OPENAMP_BIN_ROOT "${CMAKE_CURRENT_BINARY_DIR}")

list (APPEND CMAKE_MODULE_PATH
  "${CMAKE_CURRENT_SOURCE_DIR}/cmake"
  "${CMAKE_CURRENT_SOURCE_DIR}/cmake/modules"
  "${CMAKE_CURRENT_SOURCE_DIR}/cmake/platforms")

include (syscheck)

set_property (GLOBAL PROPERTY OPENAMP_APP_NAME "${OPENAMP_APP_NAME}")
SET(CMAKE_RUNTIME_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/")

include (CheckIncludeFiles)
include (CheckCSourceCompiles)
include (collect)
include (options)
include (depends)

if (_AMD_GENERATED_)
  set( CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -D_AMD_GENERATED_" )
endif()

if (${CMAKE_SYSTEM_NAME} STREQUAL "FreeRTOS")
  set( CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -DUSE_FREERTOS" )
endif()

set_property (GLOBAL PROPERTY HAS_SYSTEM_DT ON)

check_symbol_exists(VERSAL_NET "bspconfig.h" IS_VERSAL_NET)
check_symbol_exists(VERSAL "bspconfig.h" IS_VERSAL)

if (IS_VERSAL_NET)
  set_property(GLOBAL PROPERTY SOC "VERSAL_NET")
elseif(IS_VERSAL)
  set_property(GLOBAL PROPERTY SOC "VERSAL")
else()
  set_property(GLOBAL PROPERTY SOC "ZYNQMP")
endif()

get_property (SOC GLOBAL PROPERTY SOC)
message("OpenAMP: SOC in build is: ${SOC}")

# Lopper plugin can generate linker meta data in below file
# This can define RSC_TABLE so include before
file (GLOB LINKER_METADATA_FILE "${CMAKE_SOURCE_DIR}/*Example.cmake")
if (EXISTS ${LINKER_METADATA_FILE})
  set_property(GLOBAL PROPERTY LINKER_METADATA_FILE ${LINKER_METADATA_FILE})
  message("OpenAMP: Linker will be configured using file: ${LINKER_METADATA_FILE}")
else()
  message("OpenAMP: Linker will be configured using static linker script for ${SOC}")
endif()

add_subdirectory (apps)
